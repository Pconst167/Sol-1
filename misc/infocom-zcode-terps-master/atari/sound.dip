	PAGE
	SBTTL "GAME-DEPENDENT SOUND LIBRARY (ATARI FOOBLITSKY)"

	; ----------------------------
	; GAME-DEPENDENT SOUND LIBRARY
	; ----------------------------

STABLE:	DW	SND1
	DW	SND2
	DW	SND3
	DW	SND4

SOUNDS	EQU	4		; # SOUNDS IN LIBRARY

	; ----------------
	; SOUND #1: "BOOP"
	; ----------------

SND1:	LDX	#150		; FREQUENCY
	LDA	#252		; DURATION (4 JIFFIES)
	LDY	#$AE		; FULL VOLUME, NO DISTORTION
	BNE	NOISE

	; -----------------
	; SOUND #2: "CLICK"
	; -----------------

SND2:	LDX	#25		; FREQUENCY
	LDA	#255		; DURATION (1 JIFFY)
	LDY	#$AE		; FULL VOLUME, NO DISTORTION

	; FALL THROUGH ...

NOISE:	STX	AUDF1
	STY	AUDC1
	STA	RTCLOK		; START TIMER

S1L:	LDA	RTCLOK
	BNE	S1L
	STA	AUDC1		; STOP SOUND
	RTS

	; ----------------
	; SOUND #3: "TING"
	; ----------------

SND3:	LDA	#10		; FREQUENCY
	STA	AUDF1
	LDA	#$AE		; FULL, PURE TONE
	STA	AUDC1

	LDA	#251		; 5-JIFFY SUSTAIN
	STA	RTCLOK
S3L1:	LDA	RTCLOK
	BNE	S3L1

	; DECAY TO ZERO VOLUME

	LDX	#$0E		; VOLUME INDEX
S3L2:	TXA
	ORA	#$A0		; SUPERIMPOSE DISTORTION (PURE)
	STA	AUDC1

	LDA	#252		; WAIT 4 JIFFIES ON EACH LEVEL
	STA	RTCLOK
S3L3:	LDA	RTCLOK
	BNE	S3L3

	DEX
	DEX			; LOOP BACK TILL
	BNE	S3L2		; NO MORE VOLUME
	STX	AUDC1		; STOP TONE
	RTS

	; ----------------
	; SOUND #4: "RAZZ"
	; ----------------

SND4:	LDA	#100		; FREQUENCY
	STA	AUDF1
	LDA	#$2E		; LOUD, DISTORTED TONE
	STA	AUDC1

	LDA	#251		; 5-JIFFY SUSTAIN
	STA	RTCLOK
S4L1:	LDA	RTCLOK
	BNE	S4L1

	; DECAY TO ZERO VOLUME

	LDX	#$0E		; VOLUME INDEX
S4L2:	TXA
	ORA	#$20		; SUPERIMPOSE DISTORTION
	STA	AUDC1

	LDA	#252		; WAIT 4 JIFFIES ON EACH LEVEL
	STA	RTCLOK
S4L3:	LDA	RTCLOK
	BNE	S4L3

	DEX
	DEX			; LOOP BACK TILL
	BNE	S4L2		; NO MORE VOLUME
	STX	AUDC1		; STOP TONE
	RTS

	END

